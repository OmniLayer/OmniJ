buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.asciidoctor:asciidoctorj-diagram:1.5.0'
    }
}

plugins {
    id 'org.asciidoctor.convert'        version '1.5.3'
    id 'com.jfrog.bintray'              version '1.7.3'
}

// See gradle.properties for some dependency version settings

allprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    //apply plugin: 'findbugs'
    //apply plugin: 'pmd'

    version = omnijVersion
    group = 'foundation.omni'

    repositories {
        jcenter()
        //mavenLocal()   // Uncomment to work with local snapshots
        maven { url 'https://dl.bintray.com/msgilligan/maven' } // bitcoinj-addons (RPC client)
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled false
            html.enabled true
        }
    }

    tasks.withType(JavaCompile) {
        doFirst {
            if (sourceCompatibility == '1.7' && System.env.JDK7_HOME != null) {
                options.bootClasspath = "$System.env.JDK7_HOME/jre/lib/rt.jar"
                //options.bootClasspath += "$File.pathSeparator$System.env.JDK7_HOME/jre/lib/jce.jar"
                // use the line above as an example to add jce.jar
                // and other specific JDK jars
            }
        }
    }

    tasks.withType(GroovyCompile) {
        groovyOptions.optimizationOptions.indy = true
    }
}

subprojects {
    dependencies {
        compile "org.bitcoinj:bitcoinj-core:${bitcoinjVersion}"
        compile "org.slf4j:slf4j-api:${slf4jVersion}"
        
        testCompile "org.codehaus.groovy:groovy:${groovyVersion}:indy"
        testCompile("org.spockframework:spock-core:${spockVersion}") {
            exclude module: "groovy-all"
        }
        testRuntime "org.slf4j:slf4j-jdk14:${slf4jVersion}"     // Runtime implementation of slf4j for tests
    }

    compileJava {
        options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
    }

    test {
        testLogging.showStandardStreams = true
    }

}

apply from: 'gradle/idea.gradle'
apply from: 'gradle/groovydoc.gradle'
apply from: 'gradle/asciidoctor.gradle'
apply from: 'gradle/bintray.gradle'

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

task buildCI(dependsOn: [build, testReport, groovydocAll, asciidoctor])
task jenkinsBuild(dependsOn: buildCI)

