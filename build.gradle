buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'org.ajoberstar.git-publish'  version '2.1.3'
}

// See gradle.properties for some dependency version settings

buildScan {
    if (System.getenv('CI')) {
        publishAlways()
        uploadInBackground = false
        tag 'CI'
    }
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    //apply plugin: 'findbugs'
    //apply plugin: 'pmd'

    version = omnijVersion
    group = 'foundation.omni'

    repositories {
        mavenCentral()
        if (useMavenLocal == "true") {
            mavenLocal()
        }
        maven { url 'https://gitlab.com/api/v4/projects/8482916/packages/maven' } // ConsensusJ (RPC client)
    }

//    tasks.withType(FindBugs) {
//        reports {
//            xml.enabled false
//            html.enabled true
//        }
//    }
}

subprojects {
    sourceCompatibility = 8
    targetCompatibility = 8

    dependencies {
        implementation "org.slf4j:slf4j-api:${slf4jVersion}"

        testImplementation ("${consensusjGroup}:${consensusjBtcDslArtifact}:${consensusjVersion}" , {
            // These exclusions can be removed when ConsensusJ upgrades to Groovy 4.0
            exclude group: 'org.codehaus.groovy', module: 'groovy'
            exclude group: 'org.codehaus.groovy', module: 'groovy-json'
        })
        testImplementation "org.apache.groovy:groovy:${groovyVersion}"
        testImplementation("org.spockframework:spock-core:${spockVersion}") {
            exclude module: "groovy-all"
        }
        // TODO: Remove JUnit 4 dependency (AssumptionViolatedException) in RegTests
        testImplementation "junit:junit:4.13"
        testImplementation "org.spockframework:spock-junit4:${spockVersion}"
        testRuntimeOnly "org.slf4j:slf4j-jdk14:${slf4jVersion}"     // Runtime implementation of slf4j for tests
    }

    configurations.all {
        // Ensure usage of Groovy 4.0 which has new Maven co-ordinates
        resolutionStrategy.capabilitiesResolution.all {
            if (capability.group.equals("org.codehaus.groovy")) {
                selectHighestVersion()
            }
        }
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    compileJava {
        options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
    }

    tasks.withType(AbstractArchiveTask) {
        // This should result in reproducible JAR builds
        // See: https://docs.gradle.org/current/userguide/working_with_files.html#sec:reproducible_archives
        preserveFileTimestamps = false
        reproducibleFileOrder = true
    }

    test {
        useJUnitPlatform()
        testLogging.showStandardStreams = true
    }

}

apply from: 'gradle/idea.gradle'
apply from: 'gradle/groovydoc.gradle'
apply from: 'gradle/javadoc.gradle'
apply from: 'gradle/asciidoctor.gradle'
apply from: 'gradle/bintray.gradle'
apply from: 'gradle/github-pages.gradle'

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

task buildCI(dependsOn: [build, testReport, javadocAll, groovydocAll, asciidoctor])

