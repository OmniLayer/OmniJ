// Handles publication of distributions to Bintray
subprojects {
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven-publish'

    task sourceJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourceJar
        archives javadocJar
    }

    publishing {
        repositories {
            maven {
                name = "OmniJGitHubPackages"
                url = "https://maven.pkg.github.com/OmniLayer/OmniJ"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }

        publications {
            jar(MavenPublication) {
                from components.java
            }
        }
    }

    bintray {
        user = project.hasProperty("binTrayAPIUser") ? binTrayAPIUser : 'please set binTrayAPIUser'
        key = project.hasProperty("binTrayAPIKey") ? binTrayAPIKey : 'please set binTrayAPIKey'
        dryRun = false // whether to run this as dry-run, without deploying
        publications = ['jar'] // When uploading Maven-based publication files
        pkg {
            repo = 'maven'
            userOrg = 'omni'
            name = 'omnij'
            desc = 'OmniJ - Omni Layer for Java and the JVM'
            websiteUrl = 'https://github.com/OmniLayer/OmniJ'
            issueTrackerUrl = 'https://github.com/OmniLayer/OmniJ/issues'
            vcsUrl = 'https://github.com/OmniLayer/OmniJ.git'
            licenses = ['Apache-2.0']
            labels = ['bitcoin', 'omni', 'json-rpc']
            version {
                name = project.version
                desc = 'Dangerously preliminary release.'
                released  = new Date()
                vcsTag = 'none'
            }
        }
    }
}